allprojects {
    group 'hyflex'
    version '1.0-SNAPSHOT'
    
}
   
subprojects {
    apply plugin: 'java'
   
    sourceCompatibility = 11

    repositories {
        jcenter()
    }

    dependencies {
        // Use JUnit Jupiter API for testing.
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'

        // Use JUnit Jupiter Engine for testing.
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

        // jinjava library
        implementation group: 'com.hubspot.jinjava', name: 'jinjava', version: '2.6.0'
    }

    tasks.named('test') {
        // Use junit platform for unit tests.
        useJUnitPlatform()
    }
}

// Bulk configuration for all HHs
new File("./hyflex-hyperheuristics/").listFiles().each {
    if (it.directory && new File(it, 'build.gradle').exists()) {
        project(":${it.name}") {
            repositories {
                maven {
                    url "file://$projectDir/../../maven-repo"
                }
            }
            dependencies {           
                implementation project(':hyflex')
                implementation project(':hyflex-ext')
                implementation group: 'hyflex', name: 'chesc-ps', version: '2019.03.17'
            }
        }
    }
}
